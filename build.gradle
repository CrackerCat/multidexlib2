/*
 * DexPatcher - Copyright 2015, 2016 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * DexPatcher is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

plugins {
    id 'java'
    id 'signing'
    id 'maven'
}

def dexlib2Version = '2.2.0'
def multidexlib2VersionSuffix = ''

group = 'com.github.lanchon.dexpatcher'
version = dexlib2Version + multidexlib2VersionSuffix

sourceCompatibility = '1.7'

repositories {
    jcenter()

    // Use local fork of dexlib2:
    // (https://github.com/DexPatcher/dexlib2-fork-for-dexpatcher)
    //flatDir dirs: '../dexlib2-fork-for-dexpatcher/dexlib2/build/libs'
    //flatDir dirs: '../dexlib2-fork-for-dexpatcher/util/build/libs'
}

dependencies {
    compile 'org.smali:dexlib2:' + dexlib2Version

    // Use local fork of dexlib2:
    //compile 'org.smali:dexlib2:2.1.3-dexpatcher'
    //compile 'org.smali:util:2.1.3-dexpatcher'
    //compile 'com.google.guava:guava:18.0'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

Manifest sharedManifest = manifest {
    attributes(
            'Implementation-Title': 'multidexlib2',
            'Implementation-Version': version
    )
}

jar {
    manifest.from sharedManifest
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest.from sharedManifest
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    manifest.from sharedManifest
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        pom.project {
            name 'multidexlib2'
            description 'Multi-dex extensions for dexlib2.'
            url 'https://github.com/DexPatcher/multidexlib2'

            scm {
                connection 'scm:git:git://github.com/DexPatcher/multidexlib2.git'
                developerConnection 'scm:git:ssh://github.com:DexPatcher/multidexlib2.git'
                url 'https://github.com/DexPatcher/multidexlib2/tree/master'
            }

            licenses {
                license {
                    name 'GNU General Public License (version 3 or later)'
                    url 'https://www.gnu.org/licenses/gpl.txt'
                }
            }

            developers {
                developer {
                    name 'Lanchon'
                    url 'https://github.com/Lanchon'
                }
            }
        }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication userName: ossrhUsername, password: ossrhPassword
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication userName: ossrhUsername, password: ossrhPassword
        }

        beforeDeployment {
            signing.signPom(it)
        }
    }
}
